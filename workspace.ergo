# The c++ module provides an abstraction over c++ compilation and linking.
# This module assumes binaries that are compatible with gcc flags (typically gcc and clang).

^ergo std

log = log sublog c++

# If the first argument is a truth value, return it, otherwise return the second argument.
unless = fn a b -> if :a :a :b

unless-map = fn m k else -> !if (collection:has :m :k) m::k :else

# Return the value of an environment variable, or an alternative value if not set.
env_or = fn name alternative -> value:by-content <| unless (env:get :name) :alternative

# Run an executable as a task (counting as one task).
run = fn description ^args ^{^kwargs} -> task ^{task-count = 1} :description (exec ^:args ^:kwargs):complete

filter = fn f arr -> collection:fold (fn acc v -> if (f :v) [^:acc,:v] :acc) [] :arr

parse-env-flags = fn var -> {
    strs = string:split " " (env_or :var "")
    filter (fn v -> match (value:by-content :v) {
        ="" = ()
        _ = true
    }) :strs
}

# Get default compiler/flags settings.
compiler = env:path-search <| env_or CXX c++
archiver = env:path-search <| env_or AR ar
extra-cflags = parse-env-flags CXXFLAGS
extra-lflags = parse-env-flags LDFLAGS

target-os = value:by-content <| value:cache {
    preproc = "#if defined(_WIN32)
\"windows\"
#elif defined(__linux__)
\"linux\"
#elif defined(__APPLE__)
\"mac\"
#else
\"unsupported\"
#endif"
    output = exec ^{ stdin = :preproc, env = { PATH = env:get PATH } } :compiler ^:extra-cflags -E -P - |>:stdout
    :output | string:from | string:trim | string:split "\"" |>:1
}

!log debug <| string:format "detected os: {}" :target-os

lib-path-env = fn path -> value:by-content (match :target-os {
    =mac = { DYLD_LIBRARY_PATH = :path }
    =linux = { LD_LIBRARY_PATH = :path }
    _ = { PATH = :path }
})

static-library-name = fn name -> match :target-os {
    =windows = string:format "{}.lib" :name
    _ = string:format "{}.a" :name
}

dynamic-library-name = fn name -> match :target-os {
    =windows = string:format "{}.dll" :name
    =linux = string:format "lib{}.so" :name
    =mac = string:format "lib{}.dylib" :name
    _ = :name
}

exe-name = fn name -> match :target-os {
    =windows = string:format "{}.exe" :name
    _ = :name
}

# Compile a file with the given flags.
# The project-root argument is used to ensure the file paths in compiled debug sections are user-friendly.
compile = fn project-root file untracked-flags ^flags -> value:cache <| seq ^[
    out = path:join (path:new) (string:format "{}.o" <| path:relative :project-root :file)
    fs:create-dir <| path:parent :out
    run (string:format "compiling {}" :file) ^{ env = { PATH = env:get PATH } } :compiler ^:extra-cflags -c -o :out ^(value:variable :untracked-flags) ^:flags :file
    :out
]

to-includes = fn header-dirs -> collection:fold (fn l dir -> [^:l,-I,:dir]) [] :header-dirs

with-context = fn f -> {apply-context = :f}

verify-module = fn m -> !match :m {
    {apply-context} = (),
    _ = !error:throw "not an ergo-cpp module"
}

fix = fn f -> fn ^args ^{^kwargs} -> f :f ^:args ^:kwargs

# Return a file path that depends on all include dependencies, given a map of header paths to dependencies
# that will be used for includes that do not resolve to a file.
set-file-deps = fn file untracked-cflags cflags known-headers -> {
    deps = exec ^{ env = { PATH = env:get PATH } } :compiler -MM -MG ^:untracked-cflags ^:cflags :file |>:stdout | string:from | string:trim
    [_,^parts] = string:split ":" :deps
    parts = collection:fold (fn arr p -> [^:arr,^(string:split "\\\n" :p)]) [] :parts
    parts = collection:fold (fn arr p -> [^:arr,^(string:split " " :p)]) [] :parts
    parts = collection:fold (fn arr p -> match :p { ="" = :arr, o = [^:arr,:o] }) [] :parts
    parts = value:cache <| value:variable ^{ depends = [file-deps,fs:track :file,:cflags] } :parts
    # existing files depend on the result of fs:track, otherwise look in known-headers
    depends = !collection:map (fn dep -> !if (fs:exists <| path:join :dep) (fs:track :dep) (unless-map :known-headers !:dep ())) :parts
    value:variable ^{ depends } :file
}

# Convert a path to an include string
path-to-include = fn p -> {
    [a,^rest] = path:split :p
    value:by-content <| collection:fold (fn s c -> string:format "{}/{}" :s :c) :a :rest
}

# Returns a map of include paths (as strings) to tracked header files.
make-header-paths = fix (fn self dir descr -> {
    basename = fn f -> { [^_,f] = path:split :f; path:join :f }
    !match !:descr {
        =auto = value:by-content <| collection:map (fn a -> {from = :a, to = basename :a}) <| fs:glob <| path:join :dir *.h
        [^files] = value:by-content <| collection:map (fn a -> {from = :a, to = basename :a}) :files
        {^dirs} = {
            !collection:fold (fn ret {key,value} -> [^:ret,^{
                paths = self :self :dir :value
                !collection:map (fn {from,to} -> {from,to = path:join :key :to}) :paths
            }]) [] <| collection:entries :dirs
        }
    }
})

# Create a module in the given directory (used for auto parameters) with the given configuration.
#
# The configuration may be "auto", which will get all .cpp and .h files as the
# (non-public) module contents.  Otherwise, it should be a map with the
# following optional keys:
#   files: the files to compile, or "auto"
#   headers: the headers to expose
#   uses: the modules to use with this module
#
# Headers may be an array of files, "auto", or a map where the keys represent a
# directory, and values are "auto" or an array of files. "auto" will use all
# *.h files in the module path.
#
# The returned module generator function, when passed a compilation context,
# will return a module output map with the following keys:
#   impl: an array of compiled object files
#   header-dirs: an array of header directories exported by the module (and all directories from used modules)
#   runtime: an array of any additional requirements of the module (inherited from used modules)
module = fn dir config -> {
    config = match :config {
        =auto = { files = auto, headers = auto }
        other = :other
    }

    default = fn name -> unless-map :config :name []
    files = default files
    headers = default headers
    uses = default uses

    files = match (value:by-content :files) {
        =auto = value:by-content <| fs:glob <| path:join :dir *.cpp
        other = :other
    }
    headers = make-header-paths :dir (value:by-content :headers)

    # verify uses are modules
    !collection:map :verify-module :uses

    with-context (fn context -> {
        uses = !collection:map (fn u -> u:apply-context :context) :uses
        header-dirs = collection:fold (fn l use -> [^:l,^use:header-dirs]) [] :uses
        header-dir = value:cache <| seq ^[
            dir = (path:new)
            fs:create-dir :dir
            collection:map (fn {from,to} -> { !log debug <| string:format "{}" :from; fs:copy (fs:track :from) (path:join :dir :to) }) :headers
            :dir
        ]
        header-dirs = [:header-dir,^:header-dirs]
        includes = to-includes :header-dirs

        # Create maps from header path to dependency
        uses-header-deps = !collection:fold (fn m use -> !if (collection:has :use header-deps) {^:m,^use:header-deps} :m) {} :uses
        mod-header-deps = !collection:fold (fn m {from,to} -> {^:m, (path-to-include :to) = set-file-deps :from [-I,:header-dir] context:cflags :uses-header-deps}) {} :headers
        header-deps = {^:uses-header-deps,^:mod-header-deps}

        objects = collection:map (fn f -> compile context:project-root (set-file-deps :f [] context:cflags :header-deps) :includes ^context:cflags) :files
        impl = collection:fold (fn l use -> [^:l,^use:impl]) :objects :uses
        runtime = collection:fold (fn l use -> [^:l,^use:runtime,^(value:by-content <| unless-map :use runtime-if-used [])]) [] :uses
        { impl, header-dirs, header-deps, runtime }
    })
}

# Link a module.
link = fn ^{^kwargs} name mod additional-flags type include-headers -> {
    verify-module :mod
    with-context (fn context -> {
        out = (path:new)
        file = path:join :out :name
        kwcflags = value:by-content <| unless-map :kwargs cflags []
        kwlflags = value:by-content <| unless-map :kwargs lflags []
        cflags = ![^context:cflags,^:kwcflags]
        lflags = ![^context:lflags,^:kwlflags]
        mod = mod:apply-context {project-root = context:project-root, cflags, lflags}

        file = value:cache <| seq ^[
            fs:create-dir :out
            run (string:format "linking {} {}" :type :name) ^{ env = { PATH = env:get PATH } } :compiler ^:extra-lflags ^:additional-flags ^:lflags -o :file "-Wl,--start-group" ^mod:impl "-Wl,--end-group"
            :file
        ]
        ret = { file, impl = ![:file,^mod:runtime], runtime = mod:runtime, runtime-if-used = [:file] }
        !if :include-headers { ^:ret, header-deps = mod:header-deps, header-dirs = mod:header-dirs } :ret
    })
}

# Link a module into an executable.
exe = fn ^{^kwargs} name mod -> {
    mod = link ^:kwargs (exe-name :name) :mod [] executable ()
    with-context (fn context -> {
        ret = mod:apply-context :context
        {
            ^:ret
            exec = fn ^args ^{^kwargs} -> {
                libpath = value:cache <| seq ^[
                    libpath = (path:new)
                    fs:create-dir :libpath
                    collection:map (fn f -> fs:copy :f :libpath) <| value:by-content ret:runtime
                    :libpath
                ]
                exec ^{ env = {}, ^:kwargs, env = { ^:env, ^(lib-path-env :libpath) } } ret:file ^:args
            }
        }
    })
}

# Link a module into a dynamic library.
dynamic-library = fn ^{^kwargs} name mod -> link ^{ cflags = [], ^:kwargs, cflags = [^:cflags,-fPIC] } (dynamic-library-name :name) :mod [-shared] "dynamic library" true

# Link a module into a static library.
static-library = fn ^{cflags} name mod -> {
    verify-module :mod
    with-context (fn context -> {
        out = (path:new)
        fs:create-dir :out
        filename = static-library-name :name
        out = path:join :out :filename
        cflags = if :cflags [^context:cflags,^:cflags] context:cflags
        mod = mod { ^:context, cflags }
        run (string:format "creating static library {}" :filename) :archiver -crs :out ^mod:impl
        { file = :out, impl = [:file], runtime = mod:runtime, header-dirs = mod:header-dirs, header-deps = mod:header-deps }
    })
}

{
    module
    exe
    dynamic-library
    static-library
    apply-context = fn project-root cflags lflags t -> { verify-module :t; t:apply-context {project-root,cflags,lflags} }
    name = {
        static-library = :static-library-name
        dynamic-library = :dynamic-library-name
        exe = :exe-name
    }
    target-os
}
