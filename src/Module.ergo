std:import { :Array, :Iter, :Path, :Unset, :default } = $std
std:import { :context } = ergo context

Self = std:Type:new "ergo-cpp:Module"

## Visit modules and their uses.
##
## Arguments: `(Map :config) ^:modules`
##
## `config` may contain any of the following optional keys:
## * `filter` - a function that is called with two arguments to determine which dependent
## modules to visit:
##   * `module` - the module map
##   * `use` - each `uses` of the module
## (defaults to always true).
## * `map` - a function that is called on each module's inner Map and should return a type `T`
## (defaults to returning the Map as the single element within an Array).
## * `reduce` - a function called on an Iter of `T` that should return `T` (defaults to
## `Iter:flatten`, assuming `T` as `into $$Iter`).
##
## Notice that if `config` is left as all defaults, the result is all inner Maps of the modules
## being returned as an Iter.
##
## Returns `T`.
visit = fn {
    ~(default :filter as (_ -> std:Bool:true))
    map = default :map as (fn :m -> [$m])
    ~(default :reduce as Iter:flatten)
} ^:modules -> {
    do-visit = std:recurse <| :self -> fn ^:modules -> {
        reduce <| Iter:flatten <| Iter:map (fn (Self:@ :m) -> {
            reduced-uses = (self ^<| Array:from
                <| Iter:map (fn :u -> u:module)
                <| Iter:filter (fn :u -> filter $m $u)
                <| std:Unset:or m:uses [])
            [map $m, $reduced-uses]
        }) <| Array:from $modules
    }
    do-visit ^modules
}

## Create a compile_commands.json file for all compile commands in the modules.
##
## Arguments: `^(Array:of $$Module |> :modules)`
##
## Returns a Path to a compile_commands.json file.
compile-commands = fn ^:modules -> {
    path = Path:for <| fn :path -> {
        file = Path:join $path compile_commands.json
        commands = Array:from <| Iter:unique {
            visit {
                map = fn :m -> std:match m:compile-commands [
                    Array :cc -> $cc
                    _ -> []
                ]
            } ^modules
        }
        std:fs:create-dir $path
        std:fs:write (Path:join $path compile_commands.json) <| std:json:stringify ~pretty $commands
    }
    Path:owned <| Path:join $path compile_commands.json
}

## Get the runtime files for the given modules.
##
## Arguments: `^(Array:of $$Module |> :modules)`
##
## Returns an Iter of the runtime files.
runtime = fn ^:modules -> Iter:unique <| visit {
    map = fn :m -> std:match m:runtime [
        Array :rt -> $rt
        _ -> []
    ]
} ^modules

## Execute the output of a module.
##
## Arguments: `:module ^:args`
##
## Keyword Arguments:
## * `:runtime-uses` - an array of extra modules whose runtime should also be used as part of the
## executable's runtime
##
## The module must provide an executable `output` key in the result (like `exe` does).
##
## Returns the `Child` result of running `std:exec`.
exec = fn (Self :module) ~(default :runtime-uses as []) ^:args -> {
    runtime = runtime $module ^runtime-uses
    libpath = std:cache <| Path:for ~owned <| fn :libpath -> {
        std:fs:create-dir $libpath # Ensure path exists if empty
        Array:from <| Iter:map (fn :f -> std:fs:copy $f $libpath) $runtime
    }
    rt-env = std:match std:env:os [
        windows -> { PATH = $libpath }
        macos -> { DYLD_LIBRARY_PATH = $libpath }
        linux -> { LD_LIBRARY_PATH = $libpath }
    ]
    Self:@ { :output, ^_ } = $module
    std:exec $output ^(std:merge $args <| fn ~env=$rt-env)
}

##std:doc:module
## The Module type.
##
## Conceptually, a module aligns with the c++ module proposal; it represents everything needed for a
## certain set of functionality to be exposed to other modules (i.e. other code). It groups the
## exposed interfaces (headers) and the compiled code (objects/libraries/etc) that implements the
## interfaces.
##
## Modules also include the Modules that they depend upon. This approach is more convenient than
## each Module flattening dependent Module information, as retaining the dependency tree is valuable
## to various other operations. Retaining the dependency tree also allows custom associated data to
## be aggregated in application-specific ways, without having to e.g. define associated semigroup
## join functions.
##
## The type holds a single value that should evaluate to a Map with the following optional keys:
## * `Array :include` - header directories that need to be exposed to expose the module's features.
## The contents must be Maps with the following keys:
##   * `Path :dir` - the directory to include
##   * `Map :files` (optional) - a mapping from include paths (as used in `#include "PATH"`) to the
##   files to which they resolve. If this information is present, dependency checking will
##   use the resolved files from the map and will also be able to check for dependency mistakes
##   (like the use of private headers).
## * `Array :link` - files that need to be linked (or link flags) to implement the module's
## features. Elements should be `ModuleLink` values.
## * `Array :uses` - the modules that this module uses. Each item must be a Map with the following
## keys:
##   * `Module :module` - the module
##   * `:hide-include` - if set, indicates the module's `include`s (and those of any modules the
##   module uses) won't be exposed.
##   * `:hide-link` - if set, indicates the module's `link`s (and those of any modules the module
##   uses) won't be exposed.
##   * The Map may contain other custom associative information.
## * `Array :runtime` - files needed by the module's `link` components at runtime but that don't
## need to be linked. Note that files that may not be directly needed by the `link` components (like
## abi version symlinks) are appropriate here too.
## * `Array :compile-commands` - compile commands associated with the module. Each value should be a
## Map with the following keys (per `compile_commands.json` format):
##   * `Array:of $$String |> :arguments` - the arguments (including compiler)
##   * `String :file` - the input file
##   * `String :output` - the output file
##   * `String :directory` - the working directory ("/" if paths are absolute)
## * `:output` - module-defined output value, e.g. a linked file
## * `Map :custom` - extra information to add to the module. It is often best to namespace your
## particular custom information in a key that's unlikely to be used by others.
##
## Once a module is created, it can be passed to other modules but should never be
## evaluated/destructured until a context has been bound with `bind-context`.
std:Type:modify $Self {
    ## Create a new module. See `Module` documentation.
    ##
    ## Arguments: `:content`
    new = fn :v -> Self:new <| std:if $context $v else (std:Error:new "Modules must have a context bound")
    @ = Self:@
    visit
    compile-commands
    runtime
    exec
}
